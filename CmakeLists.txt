# Set the minimum required version of CMake
cmake_minimum_required(VERSION 3.22)

# Define the project name
set(PROJECT_NAME sqlogger)

# Initialize the project
project(${PROJECT_NAME})

# Set the version numbers for the project
set(SQLOGGER_VERSION_MAJOR 1)
set(SQLOGGER_VERSION_MINOR 8)
set(SQLOGGER_VERSION_BUILD 0)
set(SQLOGGER_VERSION_FULL ${SQLOGGER_VERSION_MAJOR}.${SQLOGGER_VERSION_MINOR}.${SQLOGGER_VERSION_BUILD})
set(SQLOGGER_DESCRIPTION "SQLogger is a fast, lightweight logging library that uses SQLite (optional MySQL and PostgreSQL) as a backend.")

# Configure the version header file
configure_file("sqlogger_config.h.in" "sqlogger_config.h" @ONLY)

# Set the C++ standard to C++17
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Include the current directory in the include path
set(CMAKE_INCLUDE_CURRENT_DIR ON)

# Add the include directory for the project
include_directories(${CMAKE_CURRENT_SOURCE_DIR}/include/)

# Define CMake options
option(BUILD_SHARED_LIBS "Build shared libraries" ON)
option(USE_SYSTEM_SQLITE "Use system SQLite" OFF)
option(BUILD_TEST "Build test application" ON)
option(USE_MYSQL "Enable MySQL support" OFF)
option(USE_POSTGRESQL "Enable PostgreSQL support" OFF)
#option(USE_MONGODB "Enable MongoDB support" OFF)
option(BUILD_DOC "Build documentation using Doxygen" OFF)
option(USE_SOURCE_INFO "Enable SOURCE INFO support" OFF)
option(USE_AES "Enable AES encription" OFF)


# Configure symbol export for Windows
if (WIN32)
    set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
endif()

# Find and configure SQLite3 library
if (USE_SYSTEM_SQLITE)
    find_package(SQLite3 REQUIRED)
    if (SQLite3_FOUND)
        message(STATUS "SQLite3 found: ${SQLite3_VERSION} ${SQLite3_INCLUDE_DIR}")
        include_directories(${SQLite3_INCLUDE_DIR})
    endif()
else()
    # Use the bundled SQLite amalgamation
    add_subdirectory(./3rdparty/sqlite-amalgamation)
    message(STATUS "Use bundled SQLite3 amalgamation")
endif()

if (BUILD_SHARED_LIBS)
    set(OPENSSL_USE_STATIC_LIBS FALSE) # Since CMake 3.4
else()
    set(OPENSSL_USE_STATIC_LIBS TRUE) # Since CMake 3.4
endif()

if (USE_AES)
    find_package(OpenSSL REQUIRED)
    if (OpenSSL_FOUND)
        message(STATUS "AES Support: ON")
        message(STATUS "OpenSSL found: ${OPENSSL_VERSION} ${OPENSSL_ROOT_DIR} ${OPENSSL_INCLUDE_DIR} ${OPENSSL_LIBRARIES}")
        include_directories(${OPENSSL_INCLUDE_DIR})
    else()
        message(FATAL_ERROR "OpenSSL NOT found")
    endif()
endif()

if(USE_SOURCE_INFO)
    message(STATUS "Source Info: ON")
else()
    message(STATUS "Source Info: OFF")
endif()

if(USE_SOURCE_INFO)
    add_subdirectory(./3rdparty/stduuid)
endif()

# Find and configure MySQL library (if USE_MYSQL is enabled)
if (USE_MYSQL)
    # Platform-specific paths for MySQL
    if (WIN32 AND MSVC)
        # Paths for Windows
        set(MYSQL_INCLUDE_DIR "C:/Program Files/MySQL/MySQL Server 8.0/include")
        if (BUILD_SHARED_LIBS)
            set(MYSQL_LIBRARY "C:/Program Files/MySQL/MySQL Server 8.0/lib/libmysql.lib")
        else()
            set(MYSQL_LIBRARY "C:/Program Files/MySQL/MySQL Server 8.0/lib/mysqlclient.lib")
        endif()
    elseif(UNIX AND NOT APPLE)
        # Paths for Linux
        set(MYSQL_INCLUDE_DIR "/usr/include/mysql")
        if (BUILD_SHARED_LIBS)
            set(MYSQL_LIBRARY "/usr/lib/x86_64-linux-gnu/libmysqlclient.so")
        else()
            set(MYSQL_LIBRARY "/usr/lib/x86_64-linux-gnu/libmysqlclient.a")
        endif()
    else()
         message(FATAL_ERROR "MySQL build for this platform not supported jet. Set USE_MYSQL option to OFF.")
    endif()

    # Check if MySQL headers and library exist
    if (EXISTS ${MYSQL_INCLUDE_DIR} AND EXISTS ${MYSQL_LIBRARY})
        set(MySQL_FOUND TRUE)
        set(MySQL_INCLUDE_DIRS ${MYSQL_INCLUDE_DIR})
        set(MySQL_LIBRARIES ${MYSQL_LIBRARY})
        message(STATUS "MySQL found: ${MySQL_INCLUDE_DIRS}, ${MySQL_LIBRARIES}")
    else()
        message(FATAL_ERROR "MySQL not found. Please install MySQL development libraries.")
    endif()
endif()

# Find and configure PostgreSQL library (if USE_POSTGRESQL is enabled)
if (USE_POSTGRESQL)
    find_package(PostgreSQL REQUIRED)
    if (PostgreSQL_FOUND)
        message(STATUS "PostgreSQL found: ${PostgreSQL_VERSION_STRING} ${PostgreSQL_INCLUDE_DIRS} ${PostgreSQL_LIBRARIES}")
        include_directories(${PostgreSQL_INCLUDE_DIRS})
    else()
        message(FATAL_ERROR "PostgreSQL not found. Please install PostgreSQL development libraries.")
    endif()
endif()

# Find and configure MongoDB library (if USE_MONGODB is enabled)
if (USE_MONGODB)
#    find_package(libmongoc-1.0 REQUIRED)
#    find_package(libbson-1.0 REQUIRED)
#    if (libmongoc-1.0_FOUND AND libbson-1.0_FOUND)
#        message(STATUS "MongoDB C driver found: ${LIBMONGOC_VERSION} ${LIBMONGOC_INCLUDE_DIRS} ${LIBMONGOC_LIBRARIES}")
#        message(STATUS "BSON library found: ${LIBBSON_VERSION} ${LIBBSON_INCLUDE_DIRS} ${LIBBSON_LIBRARIES}")
#        include_directories(${LIBMONGOC_INCLUDE_DIRS} ${LIBBSON_INCLUDE_DIRS})
#    else()
#        message(FATAL_ERROR "MongoDB C driver not found. Please install libmongoc and libbson.")
#    endif()
    add_subdirectory(./3rdparty/mongo-c-driver)
    message(STATUS "MongoDB Support: ON")
    message(STATUS "Use bundled MongoDB C driver")
endif()

# Define the list of header files
set(HEADERS
    "./include/logger.h"
    "./include/database_interface.h"
    "./include/sqlite_database.h"
    "./include/mock_database.h"
    "./include/log_writer.h"
    "./include/log_reader.h"
    "./include/log_entry.h"
    "./include/thread_pool.h"
    "./include/log_export.h"
    "./include/fs_helper.h"
    "./include/log_config.h"
    "./include/ini_parser.h"
    "./include/log_strings.h"
    "./include/database_helper.h"
    "./include/base64.h"
    "./include/log_crypto.h"
    "./include/database_factory.h"
    "./include/query_builder.h"
    "./include/database_schema.h"
    "./include/sql_builder.h"
    "./include/log_manager.h"
)

# Define the list of source files
set(SOURCES
    "./src/logger.cpp"
    "./src/sqlite_database.cpp"
    "./src/mock_database.cpp"
    "./src/log_writer.cpp"
    "./src/log_reader.cpp"
    "./src/thread_pool.cpp"
    "./src/log_export.cpp"
    "./src/fs_helper.cpp"
    "./src/log_config.cpp"
    "./src/ini_parser.cpp"
    "./src/database_helper.cpp"
    "./src/base64.cpp"
    "./src/log_crypto.cpp"
    "./src/database_factory.cpp"
    "./src/query_builder.cpp"
    "./src/database_schema.cpp"
    "./src/sql_builder.cpp"
    "./src/log_manager.cpp"
)

# Add MySQL header and source files if USE_MYSQL is enabled
if (USE_MYSQL)
    list(APPEND HEADERS "./include/mysql_database.h")
    list(APPEND SOURCES "./src/mysql_database.cpp")
endif()

# Add PostgreSQL header and source files if USE_PostgreSQL is enabled
if (USE_POSTGRESQL)
    list(APPEND HEADERS "./include/postgresql_database.h")
    list(APPEND SOURCES "./src/postgresql_database.cpp")
endif()

# Add MongoDB header and source files if USE_MONGODB is enabled
if (USE_MONGODB)
    list(APPEND HEADERS "./include/mongodb_database.h")
    list(APPEND SOURCES "./src/mongodb_database.cpp")
    list(APPEND HEADERS "./include/document_builder.h")
    list(APPEND SOURCES "./src/document_builder.cpp")
endif()

# Create the library (shared or static based on BUILD_SHARED_LIBS)
if (BUILD_SHARED_LIBS)
    add_library(${PROJECT_NAME} SHARED ${HEADERS} ${SOURCES})
else()
    add_library(${PROJECT_NAME} STATIC ${HEADERS} ${SOURCES})
endif()

# Library target
target_include_directories(${PROJECT_NAME} PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:include>)

# Link SQLite3 library
if (USE_SYSTEM_SQLITE)
    target_link_libraries(${PROJECT_NAME} PRIVATE SQLite::SQLite3)
else()
    if (BUILD_SHARED_LIBS)
        target_link_libraries(${PROJECT_NAME} PRIVATE sqlite3-shared)    
    else()
        target_link_libraries(${PROJECT_NAME} PRIVATE sqlite3-static)
    endif()
endif()

# Link MySQL library if USE_MYSQL is enabled
if (USE_MYSQL)
    target_link_libraries(${PROJECT_NAME} PRIVATE ${MySQL_LIBRARIES})
endif()

# Link PostgreSQL library if USE_POSTGRESQL is enabled
if (USE_POSTGRESQL)
    target_link_libraries(${PROJECT_NAME} PRIVATE ${PostgreSQL_LIBRARIES})
endif()

# Link MongoDB library if USE_MONGODB is enabled
if (USE_MONGODB)
    if (BUILD_SHARED_LIBS)
        target_link_libraries(${PROJECT_NAME} PRIVATE mongoc_shared)
    else()
        target_link_libraries(${PROJECT_NAME} PRIVATE mongoc_static)
    endif()
endif()

if (USE_SOURCE_INFO)
    target_link_libraries(${PROJECT_NAME} PRIVATE stduuid)
endif()

# Link OpenSSL library if USE_AES is enabled
if (USE_AES)
    target_link_libraries(${PROJECT_NAME} PRIVATE OpenSSL::Crypto)
    if (MSVC)
        target_link_libraries(${PROJECT_NAME} PRIVATE OpenSSL::applink)
    endif()
endif()

# Link additional libraries for Windows
if (WIN32)
   target_link_libraries(${PROJECT_NAME} PRIVATE Rpcrt4)
endif()

# Configure symbol export for Windows
if (WIN32 AND BUILD_SHARED_LIBS)
    target_compile_definitions(${PROJECT_NAME} PRIVATE LOGGER_EXPORTS)
endif()

if (USE_SOURCE_INFO)
    target_compile_definitions(${PROJECT_NAME} PRIVATE USE_SOURCE_INFO)
endif()

if (USE_AES)
    target_compile_definitions(${PROJECT_NAME} PRIVATE USE_AES)
endif()

if (USE_MYSQL)
    target_compile_definitions(${PROJECT_NAME} PRIVATE USE_MYSQL)
endif()

if (USE_POSTGRESQL)
    target_compile_definitions(${PROJECT_NAME} PRIVATE USE_POSTGRESQL)
endif()

if (USE_MONGODB)
    target_compile_definitions(${PROJECT_NAME} PRIVATE USE_MONGODB)
endif()

# Set output directories for the library
set_target_properties(${PROJECT_NAME} PROPERTIES
    LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib
    ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib
    RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin
)

# Set include directories for the library
target_include_directories(${PROJECT_NAME} PUBLIC ${CMAKE_SOURCE_DIR})
target_include_directories(${PROJECT_NAME} PUBLIC ${CMAKE_BINARY_DIR})

# Include SQLite amalgamation directory if not using system SQLite
if (NOT USE_SYSTEM_SQLITE)
    include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_SOURCE_DIR}/3rdparty/sqlite-amalgamation)
endif()

if(USE_SOURCE_INFO)
    set(UUID_SYSTEM_GENERATOR ON CACHE BOOL "Enable operating system uuid generator" FORCE)
    #include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_SOURCE_DIR}/3rdparty/stduuid/include)
    include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_SOURCE_DIR}/3rdparty/stduuid/gsl)
endif()

# Include MySQL directory
if (USE_MYSQL)
    include_directories(${PROJECT_NAME} PRIVATE ${MySQL_INCLUDE_DIRS})
endif()

if (USE_POSTGRESQL AND MSVC)
    include_directories(${PROJECT_NAME} PRIVATE ${PostgreSQL_INCLUDE_DIRS}/port/win32_msvc)
endif()

if (USE_MONGODB)
    #include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_SOURCE_DIR}/3rdparty/mongo-c-driver/src)
    include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/3rdparty/mongo-c-driver/src/libmongoc/src)
    include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_SOURCE_DIR}/3rdparty/mongo-c-driver/src/libbson/src)

    include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_BINARY_DIR}/3rdparty/mongo-c-driver/src/libmongoc/src)
    include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_BINARY_DIR}/3rdparty/mongo-c-driver/src/libbson/src)
endif()

# Install the library
install(TARGETS ${PROJECT_NAME}
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
    RUNTIME DESTINATION bin
)

# Install header files
install(FILES ${HEADERS} DESTINATION include)

# Configure and build tests if BUILD_TEST is enabled
if (BUILD_TEST)
    set(TEST_NAME test_${PROJECT_NAME})

    set(SOURCE_TEST_logger
        "test/test_logger.cpp"
    )

    if (BUILD_SHARED_LIBS)
        add_executable(${TEST_NAME} ${SOURCE_TEST_logger})
    else()
        add_executable(${TEST_NAME} ${SOURCE_TEST_logger} ${HEADERS} ${SOURCES})
    endif()

    target_link_libraries(${TEST_NAME} PRIVATE ${PROJECT_NAME})

    # Set output directory for tests
    set_target_properties(${TEST_NAME} PROPERTIES
        RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin
    )

    if (USE_SOURCE_INFO)
        target_link_libraries(${TEST_NAME} PRIVATE stduuid)
    endif()

    if (USE_AES)
        target_link_libraries(${TEST_NAME} PRIVATE OpenSSL::Crypto)
        if (MSVC)
            target_link_libraries(${TEST_NAME} PRIVATE OpenSSL::applink)
        endif()
    endif()

    if (USE_MYSQL)
        target_compile_definitions(${TEST_NAME} PRIVATE USE_MYSQL)
    endif()

    if (USE_POSTGRESQL)
        target_compile_definitions(${TEST_NAME} PRIVATE USE_POSTGRESQL)
    endif()

    if (USE_MONGODB)
        target_compile_definitions(${TEST_NAME} PRIVATE USE_MONGODB)
    endif()

    if (USE_SOURCE_INFO)
        target_compile_definitions(${TEST_NAME} PRIVATE USE_SOURCE_INFO)
    endif()

    if (USE_AES)
        target_compile_definitions(${TEST_NAME} PRIVATE USE_AES)
    endif()

    # Enable testing and add a test target
    enable_testing()
    add_test(NAME logger_test COMMAND ${TEST_NAME})

    # Install the test executable
    install(TARGETS ${TEST_NAME} RUNTIME DESTINATION bin)
endif()

# Configure Doxygen documentation generation if BUILD_DOC is enabled
if (BUILD_DOC)
    # Find Doxygen
    find_package(Doxygen REQUIRED)

    if (DOXYGEN_FOUND)
        # Set Doxygen configuration file
        set(DOXYGEN_INPUT_DIR "${CMAKE_SOURCE_DIR}/include")
        set(DOXYGEN_OUTPUT_DIR "${CMAKE_BINARY_DIR}/docs")
        set(DOXYGEN_CONFIG_FILE "${CMAKE_BINARY_DIR}/doxyfile")

        # Configure Doxyfile
        configure_file(
            "${CMAKE_SOURCE_DIR}/doxyfile.in"
            "${DOXYGEN_CONFIG_FILE}"
            @ONLY
        )

        # Add custom target for generating documentation
        add_custom_target(doc ALL
            COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_CONFIG_FILE}
            WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
            COMMENT "Generating API documentation with Doxygen"
            VERBATIM
        )

        # Install documentation
        install(DIRECTORY ${DOXYGEN_OUTPUT_DIR} DESTINATION docs)
        message(STATUS "Doxygen found: ${DOXYGEN_EXECUTABLE}, Documentation will be generated into: ${DOXYGEN_OUTPUT_DIR}")
        add_dependencies(${PROJECT_NAME} doc)
    else()
        message(WARNING "Doxygen not found, documentation will not be generated.")
    endif()
endif()
